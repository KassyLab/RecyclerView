/*
 * Copyright (C) 2018  KassyLab
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'com.android.library'

android {
    compileSdkVersion 26
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 6
        versionName "26.2.4"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    configurations {
        javadocDeps
    }
}

ext {
    bintrayRepo = 'android'
    bintrayName = 'RecyclerView'

    publishedGroupId = 'com.kassylab'
    libraryName = 'RecyclerView'
    artifact = 'recyclerview'

    libraryDescription = 'Collection of Adapter and ViewHolder designed for RecyclerView and ' +
            'RecyclerView subclass including OnItemClickListener, OnItemLongClickListener, ' +
            'OnMultiChoiceClickListener and OnItemSelectedListener.'

    siteUrl = 'https://github.com/KassyLab/RecyclerView'
    gitUrl = 'https://github.com/KassyLab/RecyclerView.git'

    libraryVersion = android.defaultConfig.versionName

    developerId = 'damien-danglard'
    developerName = 'Damien Danglard'
    developerEmail = 'damien.danglard@kassylab.com'

    organization = 'kassylab'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:recyclerview-v7:26.1.0'

    javadocDeps 'com.android.support:recyclerview-v7:26.1.0'

    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}

task updateReadme(type: Copy) {
    from('..')
    into('..')
    include('README-template.md')
    rename ('README-template.md', 'README.md')
    filter (ReplaceTokens, tokens: [version: android.defaultConfig.versionName])
}
preBuild.finalizedBy(updateReadme)

//These two remote files contain Bintray configuration as described above.
apply from: 'https://raw.githubusercontent.com/KassyLab/template-files/master/android/gradle/install.gradle'
apply from: 'bintray.gradle'
repositories {
    mavenCentral()
}
